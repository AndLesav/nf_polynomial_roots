set border lw 2
set grid lw 1.25 lt 1 lc "grey"
set key right bottom
set key font ",12"
set key box lw 1 width -1.3 height 0.5
set xtics font ",12"
set ytics font ",12" 
# set ylabel "Time" font ",17"
# set xlabel "Dimension" font ",17"


set style line 1 dt 4 pt 5 ps 1.5 lw 2 lc "black" #gp
set style line 2 dt 2 pt 9 ps 1.5 lw 2 lc "blue"   #lll abs
set style line 3 dt 5 pt 6 ps 1.5 lw 2. lc "dark-magenta"  #lll rel


file_gp(version, dg, exp) = sprintf("../data/KUMMER_GP_%s_%d_%d", version, dg, exp)
file_abs(version, dg, exp) = sprintf("../data/KUMMER_LLL_abs_%s_%d_%d", version, dg, exp)
file_rel(version, dg, exp) = sprintf("../data/KUMMER_LLL_rel_%s_%d_%d", version, dg, exp)

figure(version,exp) = sprintf("../figures/kummer_%s_%d.png", version, exp)

array EXP[4] = [2,3,5,11]
array DG[4] = [64,81,25,11]

set logscale y


# plot split

version="split"
do for [i=1:3:2] {
   exp = EXP[i]
   dg = DG[i]
   plot file_gp(version, dg, exp) u 1:2 ls 1 w lp title "nfroots",\
        file_abs(version, dg, exp) u 1:2 ls 2 w lp title "Absolute method",\
        file_rel(version, dg, exp) u 1:2 ls 3 w lp title "Relative method"

   set terminal pngcairo
   set output figure(version,exp)
   replot
}



# plot single root
version="single"
do for [i=1:4:1] {
   set key right bottom
   exp = EXP[i]
   dg = DG[i]
   plot file_gp(version, dg, exp) u 1:2 ls 1 w lp title "nfroots",\
        file_abs(version, dg, exp) u 1:2 ls 2 w lp title "Absolute method",\
        file_rel(version, dg, exp) u 1:2 ls 3 w lp title "Relative method"

   set terminal pngcairo
   set output figure(version,exp)
   replot
}


