set border lw 2
set grid lw 1.25 lt 1 lc "grey"
set key left top
set key font ",12"
set key box lw 1 width -3 height 1
set xtics font ",12"
set ytics font ",12" 
# set ylabel "Time" font ",17"
# set xlabel "Dimension" font ",17"




set style line 1 dt 4 pt 5 ps 1 lw 2.5 lc "black" #gp
set style line 2 dt 2 pt 9 ps 1 lw 2.5 lc "blue"   #lll cert
set style line 3 dt 5 pt 6 lw 2.5 lc "dark-magenta"  #lll heur
set style line 6 dt 3 pt 5 lw 2 ps 0.75 lc "olive" #gp medians



set logscale y

file_RR_gp(size) = sprintf("../data/POLFIELD_GP_real_%s", size)
file_RR_cert(size) = sprintf("../data/POLFIELD_LLL_cert_real_%s", size)
file_RR_heur(size) = sprintf("../data/POLFIELD_LLL_heur_real_%s", size)


file_CC_gp(size) = sprintf("../data/POLFIELD_GP_complex_%s", size)
file_CC_cert(size) = sprintf("../data/POLFIELD_LLL_cert_complex_%s", size)
file_CC_heur(size) = sprintf("../data/POLFIELD_LLL_heur_complex_%s", size)



figure_RR(size) = sprintf("../figures/polfield_RR_%s.png", size)
figure_CC(size) = sprintf("../figures/polfield_CC_%s.png", size)


do for [SIZE in "1 5 10"] {
   plot file_RR_gp(SIZE) u 1:2 ls 1 w lp title "nfroots - average value",\
        file_RR_gp(SIZE) u 1:3 ls 6 w lp title "nfroots - medians m_1 and m_2",\
        file_RR_gp(SIZE) u 1:4 ls 6 w lp notitle,\
        file_RR_cert(SIZE) u 1:2 ls 2 w lp title "PolynomialRoots\\\_cert",\
        file_RR_heur(SIZE) u 1:2 ls 3 w lp title "PolynomialRoots\\\_heur"

   set terminal pngcairo
   set output figure_RR(SIZE)
   replot

   plot file_CC_gp(SIZE) u 1:2 ls 1 w lp title "nfroots - average value",\
	file_CC_gp(SIZE) u 1:3 ls 6 w lp title "nfroots - medians m_1 and m_2",\
       	file_CC_gp(SIZE) u 1:4 ls 6 w lp notitle,\
	file_CC_cert(SIZE) u 1:2 ls 2 w lp title "PolynomialRoots\\\_cert",\
       	file_CC_heur(SIZE) u 1:2 ls 3 w lp title "PolynomialRoots\\\_heur"
	
   set terminal pngcairo
   set output figure_CC(SIZE)
   replot
}

