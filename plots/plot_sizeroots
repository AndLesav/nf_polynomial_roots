set border lw 2
set grid lw 1.5 lt 1 lc "grey"
set key right bottom
set key font ",12"
set key box lw 1 width -3 height 1
set xtics font ",12"
set ytics font ",12" 
# set ylabel "Time" font ",17"
# set xlabel "Dimension" font ",17"

set style line 1 dt 4 pt 5 ps 1 lw 2.5 lc "black" #gp
set style line 2 dt 2 pt 9 ps 1 lw 2.5 lc "blue"   #lll cert
set style line 3 dt 5 pt 6 lw 2.5 lc "dark-magenta"  #lll heur
set style line 6 dt 3 pt 5 lw 2  lc "olive" #gp medians




file_RR_gp(dim, size)= sprintf("../data/SIZEROOTS_GP_real_%s_%d", dim, size);
file_RR_cert(dim, size)= sprintf("../data/SIZEROOTS_LLL_cert_real_%s_%d", dim, size);
file_RR_heur(dim, size)= sprintf("../data/SIZEROOTS_LLL_heur_real_%s_%d", dim, size);

file_CC_gp(dim, size)= sprintf("../data/SIZEROOTS_GP_complex_%s_%d", dim, size)
file_CC_cert(dim, size)= sprintf("../data/SIZEROOTS_LLL_cert_complex_%s_%d", dim, size);
file_CC_heur(dim, size)= sprintf("../data/SIZEROOTS_LLL_heur_complex_%s_%d", dim, size);

figure_RR(dim, size)= sprintf("../figures/sizeroots_RR_%s_%d.png", dim, size);
figure_CC(dim, size)= sprintf("../figures/sizeroots_CC_%s_%d.png", dim, size);


set logscale y

SIZE=1				#size of pol

do for [DIM in "25 50"] {
   plot file_RR_gp(DIM,SIZE) u 1:2 ls 1 w lp title "nfroots - average value",\
   	file_RR_gp(DIM,SIZE) u 1:3 ls 6 w lp title "nfroots - medians m_1 and m_2",\
	file_RR_gp(DIM,SIZE) u 1:4 ls 6 w lp notitle,\
	file_RR_cert(DIM,SIZE) u 1:2 ls 2 w lp title "PolynomialRoots\\\_cert",\
	file_RR_heur(DIM,SIZE) u 1:2 ls 3 w lp title "PolynomialRoots\\\_heur"
   
   set terminal pngcairo
   set output figure_RR(DIM,SIZE)
   replot
}


do for [DIM in "24 50"] {
   plot file_CC_gp(DIM,SIZE) u 1:2 ls 1 w lp title "nfroots - average value",\
   	file_CC_gp(DIM,SIZE) u 1:3 ls 6 w lp title "nfroots - medians m_1 and m_2",\
     	file_CC_gp(DIM,SIZE) u 1:4 ls 6 w lp notitle,\
     	file_CC_cert(DIM,SIZE) u 1:2 ls 2 w lp title "PolynomialRoots\\\_cert",\
     	file_CC_heur(DIM,SIZE) u 1:2 ls 3 w lp title "PolynomialRoots\\\_heur"

   set terminal pngcairo
   set output figure_CC(DIM,SIZE)
   replot
}
